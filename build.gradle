plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'org.javafn'
version = "${major}.${minor}.${patch}"

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenLocal()


    mavenCentral()
}

dependencies {
    testImplementation('junit:junit:4.13.1')
}

publishing {
    publications {
        javafn(MavenPublication) {
            groupId = group
            artifactId = name
            version = version

            pom {
                name = 'JavaFn'
                description = 'Additional functional tools for Java'
                url = 'https://github.com/javafn-org/javafn'
                scm {
                    connection = 'scm:git:git://github.com:javafn-org/javafn.git'
                    developerConnection = 'scm:git:ssh://git@github.com:javafn-org/javafn.git'
                    url = 'https://github.com/javafn-org/javafn'
                }
                licenses {
                    license {
                        name = 'BSD-3-Clause'
                        url = 'https://opensource.org/license/BSD-3-clause/'
                    }
                }
                developers {
                    developer {
                        id = 'ben-stringer'
                        name = 'Ben Stringer'
                        email = 'ben.stringer.jr@javafn.org'
                    }
                    developer {
                        id = 'Archthebald'
                        name = 'Dakota Maulding'
                        email = ''
                    }
                    developer {
                        id = 'TrevorPatorno'
                        name = 'Trevor Patorno'
                        email = ''
                    }
                    developer {
                        id = 'nichipedia'
                        name = 'Nicholas Moran'
                        email = ''
                    }
                }
            }

            from components.java
        }
    }
    repositories {
        maven {
            // The official documentation on sonatype.org does not use modern gradle.
            // I based this configuration from the following
            // https://dev.to/kengotoda/deploying-to-ossrh-with-gradle-in-2020-1lhi

             url = version.endsWith("-SNAPSHOT")
                     ? "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                     : "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username = System.getenv('MAVEN_USERNAME')
                password = System.getenv('MAVEN_PASSWORD')
            }
        }
    }
}

signing {
    useGpgCmd()
    project.ext.set('signing.gnupg.executable', 'gpg')
    project.ext.set('signing.gnupg.keyName', System.getenv("OSSRH_GPG_ID"))
    project.ext.set('signing.gnupg.passphrase', System.getenv("OSSRH_GPG_SECRET_KEY_PASSWORD"))
    sign publishing.publications.javafn
}

tasks.withType(Sign) {
    onlyIf { System.getenv().containsKey("CI") }
}